buildscript {
    ext.kotlin_version = '1.1.0'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jacoco'

defaultTasks 'run'

mainClassName = 'pl.michalkowol.BootKt'
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar) {
    baseName = "${project.name}-assembly"
    manifest = jar.manifest
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task stage(dependsOn: ['fatJar', 'build', 'clean'])
build.mustRunAfter clean
fatJar.mustRunAfter build
stage.dependsOn(fatJar)

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

jacocoTestReport {
    reports {
        csv.enabled false
        html.enabled true
        xml.enabled true
    }
    dependsOn test
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    maven { url 'http://dl.bintray.com/kotlin/kotlinx.coroutines' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    ext.kotlinxcoroutines = '0.12'
    ext.rinca = '0.6.1'
    ext.itext = '7.0.2'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.1.+'

    compile 'com.sparkjava:spark-core:2.5.+'

    compile "com.github.softwareberg.rinca:database:$rinca"
    compile "com.github.softwareberg.rinca:httpclient:$rinca"
    compile "com.github.softwareberg.rinca:xml:$rinca"

    compile "com.itextpdf:sign:$itext"
    compile "com.itextpdf:root:$itext"
    compile "com.itextpdf:pdftest:$itext"
    compile "com.itextpdf:pdfa:$itext"
    compile "com.itextpdf:layout:$itext"
    compile "com.itextpdf:kernel:$itext"
    compile "com.itextpdf:io:$itext"
    compile "com.itextpdf:hyph:$itext"
    compile "com.itextpdf:forms:$itext"
    compile "com.itextpdf:font-asian:$itext"
    compile "com.itextpdf:barcodes:$itext"
    compile "com.itextpdf:itext7-core:$itext"

    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-nio:$kotlinxcoroutines"

    testCompile 'junit:junit:4.+'
    testCompile 'com.natpryce:hamkrest:1.3.+'
    testCompile 'com.nhaarman:mockito-kotlin:1.3.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}
